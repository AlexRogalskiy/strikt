buildscript {
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
    classpath "com.netflix.nebula:nebula-kotlin-plugin:1.2.41"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    classpath "com.netflix.nebula:gradle-dependency-lock-plugin:5.0.4"
//    classpath "org.ajoberstar:grgit:2.2.0"
//    classpath "org.ajoberstar:gradle-git-publish:0.4.1"
    classpath "io.spring.gradle:spring-release-plugin:0.17.1"
  }
}

apply plugin: "io.spring.release"

allprojects {
  group = "io.github.robfletcher.${rootProject.name}"

  apply plugin: "nebula.dependency-lock"
  apply plugin: "org.jetbrains.dokka"
}

//apply plugin: "org.ajoberstar.grgit"
//apply plugin: "org.ajoberstar.git-publish"

dokka {
  outputFormat = "gfm"
  jdkVersion = 8
  kotlinTasks {
    subprojects.compileKotlin
  }
}

//gitPublish {
//  branch = "gh-pages"
//  contents {
//    from "src/pages"
//    from(dokka) {
//      into "api"
//    }
//    includes = ["packages.md"]
//  }
//  commitMessage = "Publishing docs"
//}

subprojects {
  apply plugin: "io.spring.publishing"

  dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8
  }

  task(dokkaJar, type: Jar) {
    group = "documentation"
    description = "Assembles Kotlin docs with Dokka"
    classifier = "javadoc"
    from(dokka)
  }

  project.afterEvaluate {
    assemble.dependsOn sourceJar, dokkaJar
  }

  publishing {
    publications {
      nebula(MavenPublication) {
        artifact tasks.dokkaJar
      }
    }
  }

  bintray {
    bintrayUser = System.getenv("BINTRAY_USER")
    bintrayKey = System.getenv("BINTRAY_KEY")
    org = "robfletcher"
    repo = "maven"
    publication = "nebula"
    websiteUrl = "http://robfletcher.github.io/${rootProject.name}/"
    issueTrackerUrl = "https://github.com/robfletcher/${rootProject.name}/issues"
    vcsUrl = "https://github.com/robfletcher/${rootProject.name}.git"
    licenses = ["Apache-2.0"]
    labels = ["testing", "kotlin"]
  }
}
