buildscript {
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
    classpath "com.netflix.nebula:nebula-kotlin-plugin:1.2.41"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    classpath "io.spring.gradle:dependency-lock:0.1.1"
    classpath "io.spring.gradle:spring-release-plugin:0.17.1"
//    classpath "org.ajoberstar:grgit:2.2.0"
//    classpath "org.ajoberstar:gradle-git-publish:0.4.1"
  }
}

apply plugin: "io.spring.release"

allprojects {
  group = "io.github.robfletcher.${rootProject.name}"

  apply plugin: "org.jetbrains.dokka"
}

//apply plugin: "org.ajoberstar.grgit"
//apply plugin: "org.ajoberstar.git-publish"

dokka {
  outputFormat = "gfm"
  jdkVersion = 8
  kotlinTasks {
    subprojects.compileKotlin
  }
}

//gitPublish {
//  branch = "gh-pages"
//  contents {
//    from "src/pages"
//    from(dokka) {
//      into "api"
//    }
//    includes = ["packages.md"]
//  }
//  commitMessage = "Publishing docs"
//}

subprojects {
  apply plugin: "nebula.kotlin"
  apply plugin: "spring.lock"
  apply plugin: "io.spring.publishing"
  apply plugin: "org.junit.platform.gradle.plugin"

  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
  }

  dependencies {
    testCompile "org.jetbrains.spek:spek-api:+" lock '1.1.5'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:1.2.+" lock '1.2.41'
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:+" lock '1.1.5'

    // enables running in IntelliJ using JUnit runner
    testRuntime "org.junit.platform:junit-platform-launcher:+" lock '1.2.0-M1'
  }

  compileKotlin {
    kotlinOptions {
      languageVersion = "1.2"
      jvmTarget = "1.8"
    }
  }

  junitPlatform {
    filters {
      engines {
        include "spek"
      }
    }
  }

  junitPlatformTest {
    enableAssertions = true
  }

  dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8
  }

  task(dokkaJar, type: Jar) {
    group = "documentation"
    description = "Assembles Kotlin docs with Dokka"
    classifier = "javadoc"
    from(dokka)
  }

  project.afterEvaluate {
    assemble.dependsOn sourceJar, dokkaJar
  }

  publishing {
    publications {
      nebula(MavenPublication) {
        artifact tasks.dokkaJar
      }
    }
  }

  bintray {
    bintrayUser = System.getenv("BINTRAY_USER")
    bintrayKey = System.getenv("BINTRAY_KEY")
    org = "robfletcher"
    repo = "maven"
    websiteUrl = "http://robfletcher.github.io/${rootProject.name}/"
    licenses = ["Apache-2.0"]
    labels = ["testing", "kotlin"]
  }
}