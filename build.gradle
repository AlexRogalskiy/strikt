buildscript {
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
    classpath "com.netflix.nebula:nebula-kotlin-plugin:1.2.41"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    classpath "com.netflix.nebula:gradle-dependency-lock-plugin:5.0.4"
    classpath "org.ajoberstar:grgit:2.2.0"
    classpath "org.ajoberstar:gradle-git-publish:0.4.1"
    classpath "com.netflix.nebula:nebula-publishing-plugin:7.0.10"
    classpath "com.netflix.nebula:nebula-bintray-plugin:3.5.2"
  }
}

allprojects {
  apply plugin: "nebula.dependency-lock"
  apply plugin: "org.jetbrains.dokka"

  group = "io.github.robfletcher"
  version = "0.1-SNAPSHOT"
}

apply plugin: "org.ajoberstar.grgit"
apply plugin: "org.ajoberstar.git-publish"

dokka {
  outputFormat = "gfm"
  jdkVersion = 8
  kotlinTasks {
    subprojects.compileKotlin
  }
}

gitPublish {
  branch = "gh-pages"
  contents {
    from "src/pages"
    from(dokka) {
      into "api"
    }
  }
  commitMessage = "Publishing docs"
}

subprojects {
  apply plugin: "nebula.maven-publish"
  apply plugin: "nebula.nebula-bintray"
  apply plugin: "nebula.source-jar"

  dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8
    includes = ["packages.md"]
  }

  task(dokkaJar, type: Jar) {
    group = "documentation"
    description = "Assembles Kotlin docs with Dokka"
    classifier = "javadoc"
    from(dokka)
  }

  publishing {
    publications {
      nebula(MavenPublication) {
        artifact tasks.dokkaJar
      }
    }
  }

  bintray {
    pkg {
      userOrg = "robfletcher"
      repo = "maven"
      websiteUrl = "http://robfletcher.github.io/${project.name}/"
      issueTrackerUrl = "https://github.com/robfletcher/${project.name}/issues"
      vcsUrl = "https://github.com/robfletcher/${project.name}.git"
      labels = ["testing", "kotlin"]
    }
  }
}
